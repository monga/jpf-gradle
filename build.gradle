/**
 * Copyright 2017 Dan Smith
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id "java-gradle-plugin"
    id "de.undercouch.download" version "3.2.0"
    id "org.nosphere.apache.rat" version "0.3.1"
    id "com.gradle.plugin-publish" version "0.9.7"
}
import de.undercouch.gradle.tasks.download.Download

group 'com.github.upthewaterspout.jpfgradle'
version '0.3-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'


gradlePlugin {
    plugins {
        jpf {
            id = "com.github.upthewaterspout.jpf"
            implementationClass = "com.github.upthewaterspout.jpfgradle.JpfPlugin"
        }
    }
}

pluginBundle {
    website = 'https://github.com/upthewaterspout/jpf-gradle'
    vcsUrl = 'https://github.com/upthewaterspout/jpf-gradle'
    description = 'Plugin that automatically downloads and configures Java Path Finder for your gradle project'
    tags = ['jpf', 'javapathfinder']

    plugins {
        jpf {
            id = "com.github.upthewaterspout.jpf"
            displayName = 'Java Path Finder Plugin'
        }
    }
}

dependencies {
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

repositories {
    mavenCentral()
}

//Download JPF into our build directory for testing
//We use this downloaded version in our tests so that
//each test doesn't go back and download JPF every time
def jpfDownloadDir = "$buildDir/generated-resources/test"

sourceSets {
    test {
        output.dir(jpfDownloadDir, builtBy: 'downloadJpf')
    }
}

rat {
    excludes = ['build/**',
                '.gradle/**',
                '.jpf/**',
                '.idea/**',
                'README.md',
                'gradle/**',
                'gradlew*']
}

task downloadJpf(type: Download) {
    src 'https://babelfish.arc.nasa.gov/trac/jpf/raw-attachment/wiki/projects/jpf-core/jpf-core-r32.zip'
    File destFile = new File(jpfDownloadDir, "jpf-core.zip")
    dest destFile
    onlyIf { !destFile.exists() }
}
